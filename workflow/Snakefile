#!/usr/bin/env python3


include: "rules/common.smk"


rule map_bpa_metadata_to_spec:
    input:
        json="resources/datasets.jsonl.gz",
        data_mapping_config="config/data_mapping_config.json",
    output:
        metadata="results/atol_portal_metadata.csv",
        rejected_samples="results/rejected_samples.csv",
    log:
        "logs/map_bpa_metadata_to_spec.log",
    container:
        get_container("ncbi-datasets-pylib")
    script:
        "scripts/map_bpa_metadata_to_spec.py"


rule parse_bpa_json:
    input:
        json="resources/datasets.jsonl.gz",
    output:
        sample_id_dict="results/sample_id_dict.jsonl.gz",
        bioplatforms_id_dict="results/bioplatforms_id_dict.jsonl.gz",
        equivalent_samples="results/equivalent_samples.jsonl.gz",
    log:
        "logs/parse_bpa_json.log",
    container:
        get_container("ncbi-datasets-pylib")
    script:
        "scripts/parse_bpa_json.py"


rule dump_bpa:
    params:
        apikey=get_apikey(),
        url=config["bpa_url"],
    output:
        "resources/datasets.jsonl.gz",
    log:
        "logs/dump_bpa.log",
    # todo:
    # container: get_container("ncbi-datasets-pylib")
    shell:
        "ckanapi search datasets "
        " -a {params.apikey} "
        "include_private=true "
        "-O {output} "
        "-z "
        "-r {params.url} "
        "&> {log}"
